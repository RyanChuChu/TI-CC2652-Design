;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;                                                                            ;
;                                    HW4                                     ;
;                 Stepper Motor Interface specific functions                 ;
;                                                                            ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; This file contains the specific functions of the HW4 stepper motor interface 
; system. It include the following functions:
;       InitStepperGPT     - Initialize PWM timers for motor input
;       SetAngle           - Rotate the stepper motor to an absolute angle
;       SetRelAngle        - Rotate the stepper motor by a relative angle
;       HomeStepper        - Rotate the motor to the 0-degree “home” position
;       SetHomeStepper     - Redefine the current position as 0 degrees
;       GetAngle           - Return the current absolute motor angle (0–359°)
;
; Revision History:
;       12/01/25     Li-Yu Chu             initial revision
;       12/03/25     Li-Yu Chu             update comments


; local include files
        .include  "CPUreg.inc"
        .include  "GPIOreg.inc"
        .include  "IOCreg.inc"
        .include  "GPTreg.inc"
        .include  "constant.inc"
        .include  "macro.inc"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; data
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

        .data
        ;stepper motor timer initialization
        .def    InitStepperGPT

        ;global angle tracking variables for all projects
        .global CurrentPos              ;the actual current motor angle (updated by motor driver)
        .global TargetPos               ;the desired angle the motor should rotate to

        ;exported stepper motor functions
        .global SetAngle 
        .global SetRelAngle
        .global HomeStepper
        .global SetHomeStepper
        .global GetAngle

        ;LCD function
        .ref    Display                 ;display decimal string in GetAngle

        .align 4
GetAngleString: .space  BYTES_PER_WORD  ;the string storing wrapped angle returned by GetAngle

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; code
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

        .text

; InitServoGPT
;
; Description:       This function initializes GPT2 and 3. Their functions are 
;                    described below:
;                    GPT2 - two 16-bit PWM timers controlling output of DIO22 and 21
;                           (the input of A and A_bar on stepper motor)
;                    GPT3 - two 16-bit PWM timers controlling output of DIO5 and 4
;                           (the input of B and B_bar on stepper motor)
;
; Operation:         GPT2 and 3 are configured to two 16-bit timers since each output
;                    has its own PWM timer. Also, all timer is set to 10kHz PWM mode
;                    with inverted PWM output to make the duty cycle match the values
;                    in PWM table. Finally, reset all duty cycles to the second entry 
;                    of PWM table, and timers are enabled.
;
; Arguments:         None.
; Return Value:      None.
;
; Local Variables:   None.
; Shared Variables:  None.
; Global Variables:  None.
;
; Input:             None.
; Output:            DIO22, 21 - the PWM output generated by GPT2A and GPT2B
;                    DIO 5,  4 - the PWM output generated by GPT3A and GPT3B
;
; Error Handling:    None.
;
; Algorithms:        None.
; Data Structures:   None.
;
; Registers Changed: R0, R1
; Stack Depth:       0 words
;
; Known Bugs:        None.
; Limitation:        None.
;
; Revision History:  12/01/25   Li-Yu Chu       initial revision

InitStepperGPT:

GPT2Config:
        
        MOV32   R1, GPT2_BASE_ADDR              ;get GPT2 base address
        STREG   GPT_CFG_16x2, R1, GPT_CFG_OFF   ;setup two 16-bit timers

GPT2AConfig:

        STREG   GPT2A_MODE, R1, GPT_TAMR_OFF    ;set timer A mode
        STREG   PWM_ILR, R1, GPT_TAILR_OFF      ;set 16-bit timer count
        STREG   (PWM_ILR - 1), R1, GPT_TAMATCHR_OFF      ;reset duty cycle to 0

GPT2BConfig:

        STREG   GPT2B_MODE, R1, GPT_TBMR_OFF    ;set timer B mode
        STREG   PWM_ILR, R1, GPT_TBILR_OFF      ;set 16-bit timer count
        STREG   RESET_MATCHR, R1, GPT_TBMATCHR_OFF      ;reset duty cycle to 0

GPT3Config:
        
        MOV32   R1, GPT3_BASE_ADDR              ;get GPT3 base address
        STREG   GPT_CFG_16x2, R1, GPT_CFG_OFF   ;setup two 16-bit timers

GPT3AConfig:

        STREG   GPT3A_MODE, R1, GPT_TAMR_OFF    ;set timer A mode
        STREG   PWM_ILR, R1, GPT_TAILR_OFF      ;set 16-bit timer count
        STREG   RESET_MATCHR, R1, GPT_TAMATCHR_OFF      ;reset duty cycle to 0

GPT3BConfig:

        STREG   GPT3B_MODE, R1, GPT_TBMR_OFF    ;set timer B mode
        STREG   PWM_ILR, R1, GPT_TBILR_OFF      ;set 16-bit timer count
        STREG   RESET_MATCHR, R1, GPT_TBMATCHR_OFF      ;reset duty cycle to 0

GPTEnable:

        MOV32   R1, GPT2_BASE_ADDR              ;get GPT2 base address
        STREG   GPT_CTL_TABEN_PWM_INV, R1, GPT_CTL_OFF  ;enable GPT2AB with inverted PWM output

        MOV32   R1, GPT3_BASE_ADDR              ;get GPT3 base address
        STREG   GPT_CTL_TABEN_PWM_INV, R1, GPT_CTL_OFF  ;enable GPT3AB with inverted PWM output

        BX      LR                              ;done so return

; SetAngle
;
; Description:       This function rotate the stepper motor to absolute angle
;                    passed in through R0. The input angle is normalized to [0, 360) 
;                    by wraparound.
;
; Operation:         This function will wrap the angle out of range [0, 360) degrees back
;                    to the specified range. After wrapping, the result will get stored to
;                    TargetPos for event handler to recognize it.
;
; Arguments:         R0 - the absolute angle (in degrees) at which the stepper motor 
;                         is to be pointed. The non-negative angle is measured clockwise.
;                         The angle resolution is 6 degrees.
; Return Value:      None.
;
; Local Variables:   None.
; Shared Variables:  None.
; Global Variables:  TargetPos   -  the desired angle the motor should rotate to    
;                                   (initialized in main loop, updated to wrapped input
;                                   argument here)                
;
; Input:             None.
; Output:            None.
;
; Error Handling:    None.
;
; Algorithms:        The wrapping is done through loop-based angle increment and decrement
;                    arithmetic, and the stored result will affect the target position for 
;                    motor in event handler.
; Data Structures:   None.
;
; Registers Changed: flags, R0, R1
; Stack Depth:       0 words
;
; Known Bugs:        None.
; Limitation:        1. The input argument should be an integer valued angle
;                    2. It would overwrite the previous SetRelAngle targets if the stepper 
;                       motor hadn't moved to the previous target.
;                    3. The direction of movement is determined according to the current
;                       position of stepper motor. If wrapped result > current position,
;                       then it will turn clockwise, and vice versa.
;                    4. The motor only moves to the closest position if the wrapped input
;                       argument is not a multiple of the angle resolution 6°
;
; Revision History:  12/01/25   Li-Yu Chu      initial revision
;                    12/03/25   Li-Yu Chu      update header

SetAngle:

CheckSetUpperbound:
                                        ;check upper bound first
        CMP     R0, #ANGLE_FULL         ;if R0 is larger than 360°
        IT      GE
        SUBGE   R0, #ANGLE_FULL         ;subtract R0 by 360°

        BGE     CheckSetUpperbound      ;and check it again
        ;BLT    CheckSetLowerbound      ;if not, check lower bound

CheckSetLowerbound:

        CMP     R0, #HOME_POS           ;if R0 is negative
        IT      LT
        ADDLT   R0, #ANGLE_FULL         ;add R0 by 360°
        BLT     CheckSetLowerbound      ;and check it again
        ;BGE    StartSetAngle           ;if all passed, finish checking

StartSetAngle:
        
        MOVA    R1, TargetPos           ;load address of target position tracking variables
        STR     R0, [R1]                ;store new target to target position tracking variables
        
        BX      LR                      ;done so return

; SetRelAngle
;
; Description:       This function rotates the stepper motor to absolute angle passed in 
;                    through R0, which can be positive (clockwise), negative (counterclockwise), 
;                    or zero.
;
; Operation:         This function loads the current target angle from TargetPos, adds the 
;                    relative angle R0, and store it back to TargetPos. Note that we don't
;                    perform wrapping here since we want the specified relative angle can be
;                    fully represented by the movement of stepper motor. For example,
;                    SetRelAngle(720) should cause the stepper motor to turn 2 circles clockwise
;                    instead of not turning.
;
; Arguments:         R0 - the relative angle (in degrees) through which to turn the stepper 
;                         motor. A relative angle has a resolution for 4.5 degrees, and it
;                         belongs to one of the following 3 situations:
;                         1. zero (0) - indicates no movement
;                         2. positive - indicates clockwise rotation
;                         3. negative - indicates counterclockwise rotation.
; Return Value:      None.
;
; Local Variables:   None.
; Shared Variables:  None.
; Global Variables:  TargetPos   -  the desired angle the motor should rotate to    
;                                   (initialized in main loop, updated to current value
;                                   plus input argument here)
;
; Input:             None.
; Output:            None.
;
; Error Handling:    None.
;
; Algorithms:        Direct read from and write to the memory, and the stored result will 
;                    affect the target position for motor in event handler.
; Data Structures:   None.
;
; Registers Changed: R0, R1, R2
; Stack Depth:       0 words
;
; Known Bugs:        None.
; Limitation:        1. The input argument should be an integer valued angle.
;                    2. The direction of movement is determined according to the current
;                       position of stepper motor if result is stored before the stepper
;                       motor arrived the previous target. If result > current position,
;                       then it will turn clockwise, and vice versa.
;                    3. The motor only moves to the closest position if the wrapped input
;                       argument is not a multiple of the angle resolution 6°
;
; Revision History:  12/01/25   Li-Yu Chu      initial revision
;                    12/03/25   Li-Yu Chu      update header

SetRelAngle:

        MOVA    R1, TargetPos           ;load address of target position tracking variables
        LDR     R2, [R1]                ;load current target angle
        ADD     R0, R2                  ;compute new target angle
        STR     R0, [R1]                ;store new target to target position tracking variables
        
        BX      LR                      ;done so return

; HomeStepper
;
; Description:       This function sets the stepper motor to an absolute angle of 
;                    0 degrees (the "home" position). The event handler will drive 
;                    the stepper motor back to degree 0.
;
; Operation:         This function stores degree 0 (HomePos) to TargetPos.
;
; Arguments:         None.
; Return Value:      None.
;
; Local Variables:   None.
; Shared Variables:  None.
; Global Variables:  TargetPos   -  the desired angle the motor should rotate to    
;                                   (initialized in main loop, updated to 0 here)
;
; Input:             None.
; Output:            None.
;
; Error Handling:    None.
;
; Algorithms:        The stored home position in TargetPos will affect the target  
;                    position for motor in event handler.
; Data Structures:   None.
;
; Registers Changed: R0, R1
; Stack Depth:       0 words
;
; Known Bugs:        None.
; Limitation:        1. It would overwrite the previous SetRelAngle targets if the stepper 
;                       motor hadn't moved to the previous target.
;                    2. The direction of movement is determined according to the current
;                       position of stepper motor. If current position < 0, then it will
;                       turn clockwise, and vice versa.
;
; Revision History:  12/01/25   Li-Yu Chu      initial revision
;                    12/03/25   Li-Yu Chu      update header

HomeStepper:

        MOVA    R1, TargetPos           ;load address of target position tracking variables
        STREG   HOME_POS, R1, 0         ;store 0 to target position tracking variables
        
        BX      LR                      ;done so return

; SetHomeStepper
;
; Description:       This function redefines the current stepper motor angle as 
;                    the 0-degree home position. Both CurrentPos and TargetPos
;                    are reset to 0.
;
; Operation:         This function first get address of CurrentPos and TargetPos.
;                    Then we reset both CurrentPos and TargetPos to 0. Notice that
;                    even when the interrupt is triggered between the reset of 
;                    CurrentPos and TargetPos, the stepper motor still can stay
;                    in home position since the TargetPos will be 0 in next interrupt,
;                    so stepper motor can modify its position there.
;
; Arguments:         None.
; Return Value:      None.
;
; Local Variables:   None.
; Shared Variables:  None.
; Global Variables:  CurrentPos  -  the actual current motor angle
;                                   (initialized in main loop, updated to 0 here,
;                                    also updated in event handler during movement)
;                    TargetPos   -  the desired angle the motor should rotate to    
;                                   (initialized in main loop, updated to 0 here)
;
; Input:             None.
; Output:            None.
;
; Error Handling:    None.
;
; Algorithms:        Resetting both current position and target position. No modification
;                    for motor angle physically.
; Data Structures:   None.
;
; Registers Changed: R0, R1, R2
; Stack Depth:       0 words
;
; Known Bugs:        None.
; Limitation:        It will overwrite the previous SetRelAngle targets if the stepper
;                    motor hadn't moved it.
;
; Revision History:  12/01/25   Li-Yu Chu      initial revision
;                    12/03/25   Li-Yu Chu      update header

SetHomeStepper:

        MOVA    R1, CurrentPos          ;load address of current position tracking variables
        MOVA    R2, TargetPos           ;load address of target position tracking variables
        MOV32   R0, HOME_POS            ;get home position (0)
        STR     R0, [R1]                ;store 0 to current position tracking variables
        STR     R0, [R2]                ;store 0 to target position tracking variables

        BX      LR                      ;done so return

; GetAngle
;
; Description:       The function returns the current target angle setting for the stepper 
;                    motor in degrees. The angle is normalized into the [0°, 359°] range before
;                    returning. The function is not returning current motor angle since
;                    it's easily modified before returned and used in other functions, so 
;                    it returns stable expected target angle instead. The wrapped angle is 
;                    also displayed at the original position (row = 0, column = 0) on LCD.
;
; Operation:         The function first load TargetPos to R0. Then, this function performs
;                    successive increment or decrement to wrap output to [0°, 359°]. After 
;                    wrapping, it converts the normalized angle into a 3-character ASCII 
;                    decimal string stored in GetAngleString, and we pass the required 
;                    arguments into Display. Finally, it displays the string on the LCD
;                    by using Display(), and return the numeric angle in R0.
;
; Arguments:         None.
; Return Value:      R0 - the current absolute angle setting for the stepper motor in degrees. 
;                         An angle of zero (0) indicates the stepper motor is in the "home" 
;                         position and angles are measured clockwise. The value returned must 
;                         always be between 0 and 359 inclusively.
;
; Local Variables:   GetAngleString -  a 4-byte variable containing a 3-byte string with
;                                      each byte storing a decimal character, and the 
;                                      first byte is always the string length 3. The
;                                      pointer for this string will be the input argument
;                                      for Display(), which is passed by R2.
; Shared Variables:  None.
; Global Variables:  TargetPos      -  the desired angle the motor should rotate to    
;                                      (initialized in main loop, no update here)
;
; Input:             None.
; Output:            None.
;
; Error Handling:    None.
;
; Algorithms:        The wrapping is done through loop-based angle increment and decrement
;                    arithmetic. Besides, the conversion of angle to ASCII decimal string 
;                    is done by unsigned division (UDIV) to compute quotient, and multiply–
;                    subtract (MLS) for remainder.
; Data Structures:   None.
;
; Registers Changed: flags, R0, R1, R2, R6
; Stack Depth:       3 words
;
; Known Bugs:        None.
; Limitation:        None.
;
; Revision History:  12/01/25   Li-Yu Chu      initial revision
;                    12/03/25   Li-Yu Chu      update header
;                    12/05/25   Li-Yu Chu      update LCD

GetAngle:

        PUSH    {R3, R4, LR}            ;store registers
        MOVA    R1, TargetPos           ;load address of target position tracking variables
        LDR     R0, [R1]                ;load current target angle
        ;B      CheckUpperbound         ;wrap output to [0°, 359°]

CheckUpperbound:
                                        ;check upper bound first
        CMP     R0, #ANGLE_FULL         ;if R0 is larger than 360°
        IT      GE
        SUBGE   R0, #ANGLE_FULL         ;subtract R0 by 360°

        BGE     CheckUpperbound         ;and check it again
        ;BLT    CheckLowerbound         ;if not, check lower bound

CheckLowerbound:

        CMP     R0, #HOME_POS           ;if R0 is negative
        IT      LT
        ADDLT   R0, #ANGLE_FULL         ;add R0 by 360°
        BLT     CheckLowerbound         ;and check it again
        ;BGE    DoneGetAngle            ;if all passed, finish wrapping

DoneGetAngle:
                                        ;conversion of angle to ASCII decimal string
        MOVA    R2, GetAngleString      ;get address of output buffer 
        MOV     R6, R0                  ;save numeric angle (return value)

        MOV     R1, #ANGLE_STRING_LENGTH ;store string length 3 to first byte
        STRB    R1, [R2]                
                                        ;compute hundreds digit of the string
        MOV     R3, #HUNDRED            ;quotient = angle / 100
        UDIV    R1, R0, R3              

        ADD     R4, R1, #NUM_ASCII_OFF  ;convert digit to ASCII
        STRB    R4, [R2, #1]            ;store hundreds digit

                                        ;compute remainder
        MLS     R0, R3, R1, R0          ;remainder = angle - (100 * digit100)

                                        ;compute tens digit of the string
        MOV     R3, #TEN                ;quotient = remainder / 10
        UDIV    R1, R0, R3              

        ADD     R4, R1, #NUM_ASCII_OFF  ;convert digit to ASCII
        STRB    R4, [R2, #2]            ;store tens digit

                                        ;compute remainder
        MLS     R0, R3, R1, R0          ;remainder = angle - (10 * digit10)

                                        ;compute ones digit of the string
                                        ;already found as remainder
        ADD     R0, R0, #'0'            ;convert digit to ASCII
        STRB    R0, [R2, #3]            ;store ones digit
        ;B      CallDisplay

CallDisplay:

        MOV32   R0, ROW_LOW             ;set display position row = 0, column = 0
        MOV32   R1, COL_LOW
        LDRB    R4, [R2], #1            ;load string length (= 3)
        BL      Display                 ;write GetAngleString to LCD

        MOV     R0, R6                  ;restore numeric angle for return

        POP     {R3, R4, LR}            ;restore registers
        BX      LR                      ;done so return

        .end